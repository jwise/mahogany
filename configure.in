dnl $Id$
dnl Process this file with autoconf to produce a configure script.

AC_DEFUN(AC_OVERRIDES_PREPARE,
[
rm -f ${OSTYPE}.system.cache.tmp
touch ${OSTYPE}.system.cache.tmp
touch ${OSTYPE}.system.cache
])

AC_DEFUN(AC_OVERRIDES_DONE,
[
mv ${OSTYPE}.system.cache.tmp ${OSTYPE}.system.cache
])

dnl package,message,helpmessage,variable
AC_DEFUN(AC_OVERRIDES,
[
AC_MSG_CHECKING("for $2")
AC_ARG_WITH($1,$3,
[if test "x$with_$1" = xyes; then
  ac_cv_use_$1='$4="1"'
else
  ac_cv_use_$1='$4="0"'
fi],
[
  LINE=`grep "$4" ${OSTYPE}.system.cache`
  if test "x$LINE" != x ; then
    eval "DEFAULT_$LINE"
  fi
  ac_cv_use_$1='$4='$DEFAULT_$4
])
eval "$ac_cv_use_$1"
echo $ac_cv_use_$1 >> ${OSTYPE}.system.cache.tmp
if test "$$4" = 1; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
fi
])

dnl AC_CHECK_MYLIB(LIBRARY, FUNCTION, LIBPATHLIST [, ACTION-IF-FOUND 
dnl            [, ACTION-IF-NOT-FOUND [, OTHER-LIBRARIES]]])

AC_DEFUN(AC_CHECK_MYLIB,
[AC_MSG_CHECKING([for $2 in -l$1])
dnl Use a cache variable name containing both the library and function name,
dnl because the test really is for library $1 defining function $2, not
dnl just for library $1.  Separate tests with the same $1 and different $2s
dnl may have different results.
ac_lib_var=`echo $1['_']$2 | sed 'y%./+-%__p_%'`
AC_CACHE_VAL(ac_cv_lib_$ac_lib_var,
[ac_save_LIBS="$LIBS"
for j in "." $3 ; do
LIBS="-L$j -l$1 $6 $LIBS"
AC_TRY_LINK(dnl
ifelse([$2], [main], , dnl Avoid conflicting decl of main.
[/* Override any gcc2 internal prototype to avoid an error.  */
]ifelse(AC_LANG, CPLUSPLUS, [#ifdef __cplusplus
extern "C"
#endif
])dnl
[/* We use char because int might match the return type of a gcc2
    builtin and then its argument prototype would still apply.  */
char $2();
]),
	    [$2()],
            [eval "ac_cv_lib_$ac_lib_var=\"libpath_$ac_lib_var=-L$j\"" 
             eval eval "`echo '$ac_cv_lib_'$ac_lib_var`"
             LIBS="$ac_save_LIBS"
             break
            ],
	     eval "ac_cv_lib_$ac_lib_var=no")
LIBS="$ac_save_LIBS"
done
])dnl
if eval "test \"`echo '$ac_cv_lib_'$ac_lib_var`\" != no"; then
  AC_MSG_RESULT(yes)
  eval eval "`echo '$ac_cv_lib_'$ac_lib_var`"
  dnl  LIBS="`echo '$libpath_'$ac_lib_var` -l$1 $LIBS"
  ifelse([$4], ,
[changequote(, )dnl
  ac_tr_lib=HAVE_LIB`echo $1 | sed -e 's/[^a-zA-Z0-9_]/_/g' \
    -e 'y/abcdefghijklmnopqrstuvwxyz/ABCDEFGHIJKLMNOPQRSTUVWXYZ/'`
changequote([, ])dnl
  AC_DEFINE_UNQUOTED($ac_tr_lib)
  eval eval "`echo '$ac_cv_lib_'$ac_lib_var`"
  LIBS="`echo '$libpath_'$ac_lib_var` -l$1 $LIBS"
], [$4])
else
  AC_MSG_RESULT(no)
ifelse([$5], , , [$5
])dnl
fi
])


dnl ######################## Here the actual script begins ###############

AC_INIT(configure.in)
OS="${OSTYPE}"

if test "x$OS" = "x"; then
  UNAME=`uname`
  AC_MSG_ERROR("The system variable OS has not been set"
               "please set is everytime befor compiling on this system"
               "A good example for this system would be:"
               "setenv OSTYPE $UNAME        for csh as a SHELL"
               "EXPORT OSTYPE=$UNAME        for sh as SHELL"
               "please set this and restart again."
              )
fi

AC_OVERRIDES_PREPARE

DEFAULT_USE_EFENCE=0
DEFAULT_USE_DMALLOC=1
DEFAULT_USE_DEBUG=1
DEFAULT_USE_PYTHON=1
DEFAULT_USE_THREADS=1

AC_OVERRIDES(python,python,
--with-python   use python scripting,
USE_PYTHON)

AC_OVERRIDES(threads,threads,
--with-threads    link with pthread(s) library,
USE_THREADS)

AC_OVERRIDES(dmalloc,dmalloc,
--with-dmalloc  use dmalloc library for debugging,
USE_DMALLOC)

AC_OVERRIDES(efence,efence,
--with-efence   use ElectricFence,
USE_EFENCE)

AC_OVERRIDES(debug,debug,
--with-debug    use debug info/code,
USE_DEBUG)

MAKE_LOCAL_LIBS_DEFINE="junk"

AC_CONFIG_HEADER(include/config.h)


AC_CONFIG_AUX_DIR(extra/scripts)

dnl system check

AC_CANONICAL_HOST

dnl ###################
dnl # checks programs #
dnl ###################

dnl C-compiler checks
dnl =================
dnl use what compiler
AC_PROG_CC
dnl   defines CC with the compiler to use
dnl   defines GCC with yes if using gcc
dnl   defines GCC empty if not using gcc
dnl   defines CFLAGS

dnl does compiler support -c and -o simultaniously
AC_PROG_CC_C_O
dnl   defines NO_MINUS_C_MINUS_O if compiler does not accept 
dnl                              both switches simultaniously
dnl what is the c-preprocessor
AC_PROG_CPP
dnl   defines CPP with the c-preprocessor
dnl is -traditional needed for correct compilations
AC_PROG_GCC_TRADITIONAL
dnl   adds -traditional for gcc if needed

AC_LANG_SAVE

AC_PROG_INSTALL

dnl C++-compiler checks
dnl ===================
dnl use what compiler
AC_PROG_CXX
dnl   defines CXX with the compiler to use
dnl   defines GXX with yes if using gxx
dnl   defines GXX empty if not using gxx
dnl   defines CXXFLAGS
dnl what is the C++-preprocessor
AC_PROG_CXXCPP
dnl   defines CXXCPP with the C++-preprocessor

CXXFLAGS=`echo "$CXXFLAGS" | sed 's/-g./ /g'`

AC_LANG_RESTORE

AC_PROG_MAKE_SET
AC_ISC_POSIX

AC_LANG_CPLUSPLUS

AC_PATH_PROG(GREP, grep)
dnl this should be done for GXX, but we assume CC and CXX to be the same
CFLAGS=`echo "$CFLAGS" | sed 's/-g//g'`
if test "$GCC" = "yes"
then
	AC_MSG_CHECKING(for new gcc or egcs)
	CFLAGS="$CFLAGS -DCC_GCC -Wall -pedantic "
	CXXFLAGS="$CXXFLAGS -DCC_GCC -Wall -pedantic "
	newgcc=
	$CC -v 2>&1 | $GREP 2.8 >/dev/null
	if test $? = 0 ; then newgcc=yes ; fi
	$CC -v 2>&1 | $GREP egcs >/dev/null
	if test $? = 0 ; then newgcc=yes ; fi
	if test "x$newgcc" = "xyes"
	then
		CFLAGS="$CFLAGS -fno-exceptions "
		CXXFLAGS="$CXXFLAGS -fno-exceptions "
		AC_MSG_RESULT(yes)
	else
		AC_MSG_RESULT(no)
	fi
fi

dnl some extra paths
BUILDDIR=`pwd`
AC_SUBST(BUILDDIR)
LDFLAGS="$LDFLAGS -L$BUILDDIR/extra/lib"
CPPFLAGS="$CPPFLAGS -D__WXGTK__ -I$BUILDDIR/extra/include"


dnl we use C++ so we always have prototypes
AC_DEFINE(HAVE_PROTOTYPES)

works=no
AC_MSG_CHECKING(for variable length prototypes and stdarg.h)
AC_TRY_COMPILE([
#include <stdarg.h>
int foo(int x, ...) {
	va_list va;
	va_start(va, x);
	va_arg(va, int);
	va_arg(va, char *);
	va_arg(va, double);
	return 0;
}
], [return foo(10, "", 3.14);],
AC_DEFINE(HAVE_STDARG_PROTOTYPES) works=yes)
AC_MSG_RESULT($works)

if test "$have_prototypes" = yes; then
bad_prototypes=no
AC_MSG_CHECKING(for bad exec* prototypes)
AC_TRY_COMPILE([#include <unistd.h>], [char **t;execve("@",t,t);], ,
	AC_DEFINE(BAD_EXEC_PROTOTYPES) bad_prototypes=yes)
AC_MSG_RESULT($bad_prototypes)
fi

if test "$USE_WXSTRING" = 1; then
  AC_DEFINE(USE_WXSTRING)
fi

if test "$USE_WXCONFIG" = 1; then
  AC_DEFINE(USE_WXCONFIG)
fi


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_STRUCT_TM
AC_CHECK_FUNCS(strerror strstr strsep)

AC_CHECK_HEADERS(strings.h unistd.h)
AC_CHECK_HEADERS(libintl.h)
AC_CHECK_HEADERS(stdlib.h)
AC_CHECK_HEADERS(stdarg.h)


dnl extra lib dirs
EXTRA_SRC_LIB=
EXTRA_SRC_INC=
for i in `pwd`/extra/src/*
do
	LIBS="$LIBS -L$i"
	CPPFLAGS="$CPPFLAGS -I$i"
done

dnl electric fence
if test "$USE_EFENCE" = 1; then
	AC_CHECK_LIB(efence,main,,AC_MSG_WARN("efence library configured but not available."))
fi

dnl dmalloc debugging lib
if test "$USE_DMALLOC" = 1; then
	AC_CHECK_LIB(dmalloc,main,,AC_MSG_WARN("dmalloc library configured but not available."))
fi



dnl check for gtk libraries
AC_PATH_PROG(GTK_CONFIG,gtk-config,yes,no)
if test "$GTK_CONFIG" = yes
then
	GTK_CFLAGS=`gtk-config --cflags`
	GTK_LDFLAGS=`gtk-config --libs`
else
	dnl find the X11 include and library files
	AC_PATH_XTRA
	GTK_CFLAGS=$X_CFLAGS
	GTK_LDFLAGS=$X_EXTRA_LIBS
fi
LIBS="$LIBS $GTK_LDFLAGS"
AC_CHECK_LIB(dl,main,,AC_MSG_ERROR("dl library is needed."))

if test "$USE_THREADS" = 1
then
	AC_CHECK_LIB(pthread,main,,AC_CHECK_LIB(pthreads,main))
fi

AC_CHECK_HEADERS(wx/wx.h)
AC_CHECK_LIB(wx_gtk,main,,AC_MSG_ERROR("wx_gtk library is required."))

AC_CHECK_LIB(c,crypt,,AC_CHECK_LIB(crypt,crypt,,AC_MSG_ERROR("Cannot find crypt function.")))

dnl do we need this?
AC_CHECK_LIB(shadow,main)

AC_CHECK_HEADERS(libintl.h,
	AC_CHECK_LIB(c,gettext,AC_DEFINE(HAVE_GETTEXT),
		AC_CHECK_LIB(intl,gettext,AC_DEFINE(HAVE_GETTEXT))
	)
)

dnl we test for python last, because setting LIBS like this confuses further tests

MAKE_USE_PYTHON=
if test "$USE_PYTHON" = 1
then
	AC_CHECK_HEADERS(python1.5/Python.h
	,
 		AC_CHECK_MYLIB(python1.5,main,
		[        /usr \
		         /usr/lib \
		         /usr/lib/python1.5 \
		         /usr/lib/python1.5/config \
		         /usr/local \
		         /usr/local/lib \
		         /usr/local/lib/python1.5 \
		         /usr/local/lib/python1.5/config \
		         /usr/local/python1.5/config\
		],
			AC_CHECK_LIB(swigpy,main,
				AC_DEFINE(USE_PYTHON)
				MAKE_USE_PYTHON="USE_PYTHON=yes"
				LIBS="$libpath_python1_5_main -lpython1.5 -lswigpy $LIBS"
				,
				AC_MSG_WARN("Cannot find SWIG library for python - disabling python.")
				MAKE_USE_PYTHON=
				,[$libpath_python1_5_main -lpython1.5]
			)
		)			
	)
AC_PATH_PROG(SWIG,swig)
fi
AC_SUBST(MAKE_USE_PYTHON)

AC_CHECK_HEADERS(compface.h)

dnl set debug/optimisation flags
dnl we don't use CFLAGS/CXXFLAGS from autoconf
DBGFLAGS=
if test "$USE_DEBUG" = 1; then
	DBGFLAGS="-O0 -g3 -DDEBUG"
fi
AC_SUBST(DBGFLAGS)

dnl we need to set the target for c-client library:
dnl supported OSTYPE values are linux* and solaris* and freebsd*

AC_MSG_CHECKING(for c-client library OS type)
CCOSTYPE=
case "${OSTYPE}" in
linux*)
  CCOSTYPE=slx
  ;;
solaris*)
  if test "$GCC" = "yes"
  then
    CCOSTYPE=gso
  else
    CCOSTYPE=sol
  fi
  ;;
freebsd*)
  CCOSTYPE=bsf
  ;;
OSF1*)
  if test "$GCC" = "yes"
  then
    CCOSTYPE=gof
  else
    CCOSTYPE=osf
  fi
  ;;
esac

AC_MSG_RESULT($CCOSTYPE)
if test "x$CCOSTYPE" = "x" ; then
  echo "Cannot determine OS type for c-client library. Supported are linux, solaris, freebsd."
  exit 1
fi
AC_SUBST(CCOSTYPE)


DOCHTML="echo \"No doc generator(html) available.\""
DOCTEX="echo \"No doc generator(tex) available.\""

AC_PATH_PROG(DOCXX,docxx,yes,no)
if test "x$DOCXX" = "x"
then
	AC_PATH_PROG(KDOC,kdoc,yes,no)
	if test "x$KDOC" != "x"
	then
		DOCHTML="\$(KDOCHTML)"
		DOCTEX="\$(KDOCTEX)"
	fi
else
	DOCHTML="\$(DOCXXHTML)"
	DOCTEX="\$(DOCXXTEX)"
fi
AC_SUBST(DOCHTML)
AC_SUBST(DOCTEX)

AC_OUTPUT(makeopts)
